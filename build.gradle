plugins {
    id 'java'
    id 'eclipse'
    id 'idea'
    id 'com.github.sherter.google-java-format' version '0.7.1'
    id 'net.ltgt.errorprone' version '0.6'
    id 'jacoco'
}

import com.github.sherter.googlejavaformatgradleplugin.GoogleJavaFormat
import com.github.sherter.googlejavaformatgradleplugin.VerifyGoogleJavaFormat

group 'me.lukasczyk'
version '1.0-SNAPSHOT'

sourceCompatibility = 1.10

repositories {
    mavenCentral()
}

dependencies {
    compile group: 'com.google.guava', name: 'guava', version: '26.0-jre'

    errorprone("com.google.errorprone:error_prone_core:2.3.1")

    testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.3.1'
    testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: '5.3.1'

    testRuntime group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.3.1'
}

jacoco {
    toolVersion = "0.8.2"
    reportsDir = file("${buildDir}/jacoco")
}

jacocoTestReport {
    reports {
        xml.enabled = false
        csv.enabled = false
        html.enabled = true
        html.setDestination(file("${buildDir}/jacoco/html"))
    }
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }

    finalizedBy jacocoTestReport
}

tasks.withType(JavaCompile).configureEach {
    options.errorprone.disableWarningsInGeneratedCode = true
}

task format(type: GoogleJavaFormat) {
    source = ['src/main', 'src/test']
    include '**/*.java'
}

task verifyFormatting(type: VerifyGoogleJavaFormat) {
    source = 'src/main'
    include '**/*.java'
    ignoreFailures true
}

task wrapper(type: Wrapper) {
    description = 'Generates gradlew[.bat] scripts'
    gradleVersion = '4.10'
}

eclipse {
    classpath {
        downloadJavadoc = true
        downloadSources = true
    }
}

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}
